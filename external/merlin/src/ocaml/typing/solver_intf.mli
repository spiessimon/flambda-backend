(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*                   Stephen Dolan, Jane Street, London                   *)
(*                    Zesen Qian, Jane Street, London                     *)
(*                                                                        *)
(*   Copyright 2024 Jane Street Group LLC                                 *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

open Allowance
module Fmt = Format_doc

module type Equal = sig
  type ('a, 'b, 'd) t constraint 'd = 'l * 'r

  val equal :
    ('a0, 'b, 'l0 * 'r0) t ->
    ('a1, 'b, 'l1 * 'r1) t ->
    ('a0, 'a1) Misc.eq option
end

(** A collection of lattices, indexed by [obj]; *)
module type Lattices = sig
  (** Lattice identifers, indexed by ['a] the carrier type of that lattice *)
  type 'a obj

  (** An element in a lattice whose carrier type is ['a]. *)
  type 'a elt

  val min : 'a obj -> 'a elt

  val max : 'a obj -> 'a elt

  val le : 'a obj -> 'a elt -> 'a elt -> bool

  val equal : 'a obj -> 'a elt -> 'a elt -> bool

  val join : 'a obj -> 'a elt -> 'a elt -> 'a elt

  val meet : 'a obj -> 'a elt -> 'a elt -> 'a elt

  val print : 'a obj -> Fmt.formatter -> 'a elt -> unit

  (** Total ordering on objects, used for internal map keys. This is not a
      lattice ordering or semantic equality check. If this returns [0], then
      [equal_obj] must return [Misc.Is_eq]. *)
  val compare_obj : 'a obj -> 'b obj -> int

  (** Equality on objects recognized by the solver. This is the only object
      comparison that carries type equality evidence. *)
  val equal_obj : 'a obj -> 'b obj -> ('a, 'b) Misc.is_eq

  val print_obj : Fmt.formatter -> 'a obj -> unit
end

(** Extend [Lattices] with monotone functions (including identity) to form a
    category. Among those monotone functions some will have left and right
    adjoints. *)
module type Lattices_mono = sig
  include Lattices with type 'a elt := 'a

  (** Morphism from object of base type ['a] to object of base type ['b] with
      allowance ['d]. See Note [Allowance] in allowance.mli.

      ['d] is ['l] * ['r], where ['l] can be:
      - [allowed], meaning the morphism can be on the left because it has right
        adjoint.
      - [disallowed], meaning the morphism cannot be on the left because it does
        not have right adjoint. Similar for ['r]. *)
  type ('a, 'b, 'd) morph

  (* Due to the implementation in [solver.ml], a mode doesn't have sufficient
     information to infer the object it lives in,  whether at compile-time or
     runtime. There is info at compile-time to distinguish between different
     carrier types, but one can imagine multiple objects with the same carrier
     type. Therefore, we can treat modes as object-blind.

     As a result, user of the solver needs to provide the object the modes live
     in, every time it invokes the solver on some modes.

     Roughly, ['a mode] is represented in the solver as constant of ['a], or [f
     v] where [f] is a morphism from ['b] to ['a] and [v] is some variable of
     ['b]. The ['a] needs additional ['a obj] to decide its position in the
     lattice structure (because again, multiple lattices can share the same
     carrier type). One might think the morphism [f] should know its own source
     and target objects. But since its target object is already given by the
     user for each invocation anyway, we decide to exploit this, and say that "a
     morphism is determined by some [('a, 'b, 'd) morph] together with some ['b
     obj]". That helps reduce the information each [morph] needs to store.

     As a result, in the interaction between the solver and the lattices,
     [morph] always comes with its target object. *)

  (** Give the source object of a morphism *)
  val src : 'b obj -> ('a, 'b, 'd) morph -> 'a obj

  (** Give the identity morphism on an object *)
  val id : ('a, 'a, 'd) morph

  (** Compose two morphisms *)
  val compose :
    'c obj -> ('b, 'c, 'd) morph -> ('a, 'b, 'd) morph -> ('a, 'c, 'd) morph

  (* Usual notion of adjunction:
     Given two morphisms [f : A -> B] and [g : B -> A], we require [f a <= b]
      iff [a <= g b] for each [a \in A] and [b \in B].

     Our solver accepts a wider notion of adjunction: Given two morphisms [f : A
     -> B] and [g : B -> A], we require [f a <= b] iff [a <= g b] for each [a]
      in the downward closure of [g]'s image and [b \in B].

     We say [f] is a partial left adjoint of [g], because [f] is only
     constrained in part of its domain. As a result, [f] is not unique, since
     its valuation out of the constrained range can be arbitrarily chosen.

     Dually, we can define the concept of partial right adjoint. Since partial
     adjoints are not unique, they don't form a pair: i.e., a partial left
     joint of a partial right adjoint of [f] is not [f] in general.

     Concretely, the solver provides/requires the following guarantees
     (continuing the example above):

     For the user of the [Solvers_polarized].
     - [g] applied to a right mode [m] can be used as a right mode without
     any restriction.
     - [f] applied to to a left mode [m] can be used as a left mode, given that
     the [m] is fully within the downward closure of [g]. This is unfortunately
     not enforcable by the ocaml type system, and we have to rely on user's
     caution.

     For the supplier of the [Lattices_mono]:
     - The result of [left_adjoint g] is applied only on the downward closure of
     [g]'s image.
  *)

  (* Note that [left_adjoint] and [right_adjoint] returns a [morph] weaker than
     what we want, which is "\exists r. allowed * r". But ocaml doesn't like
     existentials, and this weaker version is good enough for us *)

  (** Give left adjoint of a morphism *)
  val left_adjoint :
    'b obj -> ('a, 'b, 'l * allowed) morph -> ('b, 'a, left_only) morph

  (** Give the right adjoint of a morphism *)
  val right_adjoint :
    'b obj -> ('a, 'b, allowed * 'r) morph -> ('b, 'a, right_only) morph

  include Allow_disallow with type ('a, 'b, 'd) sided = ('a, 'b, 'd) morph

  (** Apply morphism on constant *)
  val apply : 'b obj -> ('a, 'b, 'd) morph -> 'a -> 'b

  (** Total ordering on morphisms with a shared target object, used for internal
      map keys. This is not a lattice ordering or semantic equality check. If
      this returns [0], then [equal_morph] must return [Misc.Is_eq]. *)
  val compare_morph :
    'b obj -> ('a0, 'b, 'd0) morph -> ('a1, 'b, 'd1) morph -> int

  (** Equality on morphisms with a shared target object recognized by the
      solver. This is the only morphism comparison that carries source type
      equality evidence. *)
  val equal_morph :
    'b obj ->
    ('a0, 'b, 'd0) morph ->
    ('a1, 'b, 'd1) morph ->
    ('a0, 'a1) Misc.is_eq

  (** Print morphism *)
  val print_morph : 'b obj -> Fmt.formatter -> ('a, 'b, 'd) morph -> unit
end

type 'd branch =
  | Join : ('l * disallowed) branch
  | Meet : (disallowed * 'r) branch
  constraint 'd = _ * _
[@@ocaml.warning "-62"]

module type Solver_mono = sig
  (* These first few types will be replaced with types from
     the Lattices_mono *)

  (** The morphism type from the [Lattices_mono] we're working with. See
      comments on [Lattices_mono.morph]. *)
  type ('a, 'b, 'd) morph

  (** The object type from the [Lattices_mono] we're working with *)
  type 'a obj

  type pinpoint

  (** Represents a sequence of local changes to the copying scope of a mode
      variable. Copying a mode takes a [copy_scope], and it is up to the caller
      to create a new copy scope, and reset it once all copies have been made *)
  type copy_scope

  (** Runs a function [f] in a fresh mode copy scope, which gets cleaned up once
      [f] is finished *)
  val with_copy_scope : (copy_scope -> 'a) -> 'a

  type 'd hint_morph constraint 'd = 'l * 'r

  type 'd hint_const constraint 'd = 'l * 'r

  (* Backtracking facilities used by [types.ml] *)

  (** Represents a sequence of state mutations caused by mode operations. All
      mutating operations in this module take a [log:changes ref option] and
      append to it all changes made, regardless of success or failure. It is
      [option] only for performance reasons; the caller should never provide
      [log:None]. The caller is responsible for taking care of the appended log:
      they can either revert the changes using [undo_changes], or commit the
      changes to the global log in [types.ml]. *)
  type changes

  (** An empty sequence of changes. *)
  val empty_changes : changes

  (** Undo the sequence of changes recorded. *)
  val undo_changes : changes -> unit

  (** A mode with carrier type ['a] and allowance ['d]. See Note [Allowance] in
      allowance.mli.*)
  type ('a, 'd) mode constraint 'd = 'l * 'r

  include Allow_disallow with type ('a, _, 'd) sided = ('a, 'd) mode

  (** Returns the mode representing the given constant, explained by the
      optional hint. *)
  val of_const :
    'a obj -> ?hint:('l * 'r) hint_const -> 'a -> ('a, 'l * 'r) mode

  (* CR-soon zqian: [to_const_exn] should return hints as well. *)

  (** Given a mode whose lower and upper bounds are equal, returns that bound.
      Raises exception if the condition does not hold. *)
  val to_const_exn : 'a obj -> ('a, allowed * allowed) mode -> 'a

  (** The minimum mode in the lattice *)
  val min : 'a obj -> ('a, 'l * 'r) mode

  (** The maximum mode in the lattice *)
  val max : 'a obj -> ('a, 'l * 'r) mode

  (** The level of generic variables. *)
  val generic_level : int

  (** The level of rigid variables used during subsumption. *)
  val rigid_level : int

  (* CR-someday zqian: [zap_*] should take optional hint, pointing to the location in
     the source code where zapping happens *)

  (** Pushes the mode variable to the lowest constant possible. Expensive.
      WARNING: the lattice must be finite for this to terminate.*)
  val zap_to_floor :
    'a obj -> ('a, allowed * 'r) mode -> log:changes ref option -> 'a

  (** Pushes the mode variable to the highest constant possible. *)
  val zap_to_ceil :
    'a obj -> ('a, 'l * allowed) mode -> log:changes ref option -> 'a

  (** Create a new mode variable of the full range at given level. *)
  val newvar : 'a obj -> int -> ('a, 'l * 'r) mode

  (** Remove hints from all vars that have been created. This doesn't affect
      hints that were applied on top of vars. For example:

      let m = newvar () in submode m (apply ~hint:hint0 g n); let m' = apply
      ~hint:hint1 f m in erase_hints ()

      The last line erases the hints on [m] (caused by [n]), but [m'] has an
      immutable hint on top of [m], which is not affected. *)
  val erase_hints : unit -> unit

  (** Raw hint returned by failed [submode a b]. To consume it, see
      [populate_hint]. *)
  type ('a, 'd) hint_raw constraint 'd = 'l * 'r

  (** Raw error returned by failed [submode a b]. [left] will be the lowest mode
      [a] can be, and [right] will be the highest mode [b] can be. And
      [left <= right] will be false, which is why the submode failed.
      [left_hint] explains why [left] is high, and [right_hint] explains why
      [right] is low. To consume them, see [populate_hint] or [populate_error].
  *)
  type 'a error_raw =
    { left : 'a;
      left_hint : ('a, left_only) hint_raw;
      right : 'a;
      right_hint : ('a, right_only) hint_raw
    }

  (** Try to constrain the first mode below the second mode. [pinpoint]
      describes the thing that has both modes. *)
  val submode :
    pinpoint ->
    'a obj ->
    ('a, allowed * 'r) mode ->
    ('a, 'l * allowed) mode ->
    log:changes ref option ->
    (unit, 'a error_raw) result

  (** Lowers a level of a variable. *)
  val update_level :
    int -> 'a obj -> ('a, 'l * 'r) mode -> log:changes ref option -> unit

  (** Moves a variable [u] whose level is above [current_level] and below
      [generic_level] to [generic_level + i], where [i] is the difference
      between [u.level] and [current_level] (the same is then done to all of
      [u]'s children) *)
  val generalize_topology :
    current_level:int -> ('a, 'l * 'r) mode -> log:changes ref option -> unit

  (** Generalizes a variable whose level is above [current_level], by putting
      its level to [generic_level], and all its children above [current_level]
      to [generic_level + i] for some nonzero [i]. *)
  val generalize :
    current_level:int ->
    'a obj ->
    ('a, 'l * 'r) mode ->
    log:changes ref option ->
    unit

  (** Generalizes all reachable variables whose level is above [current_level],
      whose value is fully determined, by putting their level to
      [generic_level].*)
  val generalize_structure :
    current_level:int ->
    'a obj ->
    ('a, 'l * 'r) mode ->
    log:changes ref option ->
    unit

  (** Resets the counter used to allocate the (negative) ids of persistent
      copies of mode variables. Mirrors [Subst.reset_additional_action_id] for
      types, and is called at the start of every cmi save. *)
  val reset_persistent_id : unit -> unit

  (** If the variable has not yet been copied within the same [copy_scope],
      [copy] copies all reachable variables whose level lies within the window
      \[[copy_from_level], [copy_below_level]). If [copy_to_level] is given, the
      copies are placed at that level; this is only allowed for windows of size
      1). If not given, copies keep the levels of their originals. If
      [persistent] (default false), copies receive negative ids from a dedicated
      counter (see [reset_persistent_id]). Returns either the freshly copied
      mode, or the copy cached in [copy_scope]. *)
  val copy :
    copy_scope:copy_scope ->
    copy_from_level:int ->
    copy_below_level:int ->
    ?copy_to_level:int ->
    ?cause:[`Save | `Restore | `Neither] ->
    'a obj ->
    ('a, 'l * 'r) mode ->
    ('a, 'l * 'r) mode

  (** Creates a new mode variable above the given mode and returns [true]. In
      the speical case where the given mode is top, returns the constant top and
      [false]. *)
  val newvar_above :
    'a obj -> int -> ('a, allowed * 'r_) mode -> ('a, 'l * 'r) mode * bool

  (** Creates a new mode variable below the given mode and returns [true]. In
      the speical case where the given mode is bottom, returns the constant
      bottom and [false]. *)
  val newvar_below :
    'a obj -> int -> ('a, 'l_ * allowed) mode -> ('a, 'l * 'r) mode * bool

  (** Returns the join of the list of modes. *)
  val join : 'a obj -> ('a, allowed * 'r) mode list -> ('a, left_only) mode

  (** Return the meet of the list of modes. *)
  val meet : 'a obj -> ('a, 'l * allowed) mode list -> ('a, right_only) mode

  (** Returns the lower bound of the mode. Because of our conservative internal
      representation, further constraining is needed for precise bound. This
      operation is therefore expensive and requires the mode to be allowed on
      the left. WARNING: the lattice must be finite for this to terminate. *)
  val get_floor : 'a obj -> ('a, allowed * 'r) mode -> 'a

  (** Returns the upper bound of the mode. Notes for [get_floor] applies. *)
  val get_ceil : 'a obj -> ('a, 'l * allowed) mode -> 'a

  (** Similar to [get_floor] but does not run the further constraining needed
      for a precise bound. As a result, the returned bound is loose; i.e., it
      might be lower than the real floor. *)
  val get_loose_floor : 'a obj -> ('a, 'l * 'r) mode -> 'a

  (** Similar to [get_ceil] but does not run the further constraining needed for
      a precise bound. As a result, the returned bound is loose; i.e., it might
      be higher than the real ceil. *)
  val get_loose_ceil : 'a obj -> ('a, 'l * 'r) mode -> 'a

  (** Printing a mode for debugging. *)
  val print :
    ?verbose:bool -> 'a obj -> Fmt.formatter -> ('a, 'l * 'r) mode -> unit

  (** Returns true if the mode is a constant or a mode variable at level 0 *)
  val check_const_or_level_0 : ('a, 'l * 'r) mode -> bool

  (** Returns true if the mode includes a mode variable at generic level *)
  val check_generic : ('a, 'l * 'r) mode -> bool

  type var_iterator =
    { iter : 'a. 'a obj -> ('a, allowed * allowed) mode -> unit }
  [@@unboxed]

  val mode_iter : 'a obj -> ('a, 'l * 'r) mode -> var_iterator -> unit

  type 'b packed_morph = Packed_morph : ('a, 'b, 'd) morph -> 'b packed_morph

  (** Applies an iterator over every reachable covariant (left-) constraint
      variable. The iterator is only applied to constraint variables at level 0,
      and exposes the int identifier of the constraint variable. WARNING: the
      iterator is only applied once per constraint, even when it appears as a
      constraint multiple times via different morphisms *)
  val iter_covariant :
    'a obj ->
    ('a, allowed * 'r) mode ->
    (id:int ->
    level:int ->
    morph:'a packed_morph ->
    ('a, allowed * disallowed) mode ->
    unit) ->
    unit

  (** Applies an iterator over every reachable contravariant (right-) constraint
      variable. The iterator is only applied to constraint variables at level 0,
      and exposes the int identifier of the constraint variable. WARNING: the
      iterator is only applied once per constraint, even when it appears as a
      constraint multiple times via different morphisms *)
  val iter_contravariant :
    'a obj ->
    ('a, 'l * allowed) mode ->
    (id:int ->
    level:int ->
    morph:'a packed_morph ->
    ('a, disallowed * allowed) mode ->
    unit) ->
    unit

  (** Apply a monotone morphism explained by an optional hint *)
  val apply :
    'b obj ->
    ?hint:('l * 'r) hint_morph ->
    ('a, 'b, 'l * 'r) morph ->
    ('a, 'l * 'r) mode ->
    ('b, 'l * 'r) mode

  (** [('a, 'd) hint] explains a bound of type ['a] and allowance ['d], but
      doesn't contain the bound itself. *)
  type ('a, 'd) hint =
    | Apply :
        'd hint_morph * ('b, 'a, 'd) morph * ('b, 'd) ahint
        -> ('a, 'd) hint
        (** [Apply morph_hint morph x_hint] says the current bound is derived by
            applying morphism [morph] (explained by [morph_hint]) to another
            bound explained by [x_hint] *)
    | Const : 'd hint_const -> ('a, 'd) hint
        (** [Const const_hint] says the current bound is explained by
            [const_hint] *)
    | Branch : 'd branch * ('a, 'd) ahint * ('a, 'd) ahint -> ('a, 'd) hint
        (** [branch b hint0 hint1] says the current bound is jointly explained
            by [hint0] and [hint1]. *)
    constraint 'd = _ * _
  [@@ocaml.warning "-62"]

  (** [('a, 'd) ahint] is a bound of type ['a] explained by a [('a, 'd) hint].
  *)
  and ('a, 'd) ahint = 'a * ('a, 'd) hint constraint 'd = _ * _

  (** Mode error that's suitable for consumption. *)
  type 'a error =
    { left : ('a, left_only) ahint;
      right : ('a, right_only) ahint
    }

  (** Takes a bound with a [hint_raw] given by [submode], and returns a [ahint]
      that's suitable for consumption. *)
  val populate_hint :
    'a obj -> 'a -> ('a, 'l * 'r) hint_raw -> ('a, 'l * 'r) ahint

  (** Takes a [error_raw] returned by [submode], and returns an [error] hint
      that's suitable for consumption. *)
  val populate_error : 'a obj -> 'a error_raw -> 'a error

  module Unhint : sig
    (** Unhinted mode is similar to [('a, 'd) mode], but its several outermost
        morphism applications are unhinted. *)
    type ('a, 'd) t constraint 'd = 'l * 'r

    (** Treat a regular mode as an unhinted mode, by taking the identity
        morphism as the outermost unhinted morphism. *)
    val unhint : ('a, 'l * 'r) mode -> ('a, 'l * 'r) t

    (** Takes an unhinted mode, annotate the outermost unhinted morphisms (as a
        whole) with the given hint, which gives a regular mode. *)
    val hint :
      'a obj ->
      ?hint:('l * 'r) hint_morph ->
      ('a, 'l * 'r) t ->
      ('a, 'l * 'r) mode

    (** Apply another unhinted morphism to an unhinted mode. *)
    val apply :
      'b obj -> ('a, 'b, 'l * 'r) morph -> ('a, 'l * 'r) t -> ('b, 'l * 'r) t
  end

  (** The exposed description of modes *)
  module Desc : sig
    module Var : sig
      type 'a t

      type ('b, 'd) t_with_morph =
        | Amorphvar : 'a t * ('a, 'b, 'd) morph -> ('b, 'd) t_with_morph

      module Head : sig
        type 'a t =
          { desc_id : int;
            desc_upper : 'a;
            desc_lower : 'a;
            desc_vlower : ('a, left_only) t_with_morph list;
            desc_level : int
          }

        val equal : 'a t -> 'b t -> bool

        val hash : 'a t -> int
      end

      val force : 'a obj -> 'a t -> 'a Head.t
    end

    type ('b, 'd) morphvar =
      | Amorphvar : 'a Var.Head.t * ('a, 'b, 'd) morph -> ('b, 'd) morphvar

    type ('a, 'd) t =
      | Amode : 'a -> ('a, 'l * 'r) t
      | Amodevar : ('a, 'd) morphvar -> ('a, 'd) t
      | Amodejoin :
          'a * ('a, 'l * disallowed) morphvar list
          -> ('a, 'l * disallowed) t
      | Amodemeet :
          'a * ('a, disallowed * 'r) morphvar list
          -> ('a, disallowed * 'r) t

    val equal : 'a obj -> ('a, 'l * 'r) t -> ('a, 'l * 'r) t -> bool

    val print : 'a obj -> Fmt.formatter -> ('a, 'l * 'r) t -> unit
  end

  (** Returns the description of a mode. *)
  val desc : 'a obj -> ('a, 'd) mode -> ('a, 'd) Desc.t
end

(** Hint module to be provided by the user of the solver. *)
module type Hint = sig
  module Pinpoint : sig
    (** Descriptions of things that have modes. *)
    type t

    (** Something that have modes but not described. *)
    val unknown : t
  end

  module Morph : sig
    (** Hints that explain morphisms. The allowance ['d] describes if the
        morphism can be on the LHS or RHS of [submode]. *)
    type 'd t constraint 'd = 'l * 'r

    (** The hint for the identity morphism *)
    val id : 'd t

    (** Given a hint for a mode morphism with its destination pinpoint, return a
        hint for the left adjoint of the morphism with the opposite pinpoint. *)
    val left_adjoint :
      Pinpoint.t -> (_ * allowed) t -> Pinpoint.t * (allowed * disallowed) t

    (** Given a hint for a mode morphism with its destination pinpoint, return a
        hint for the right adjoint of the morphism with the opposite pinpoint.
    *)
    val right_adjoint :
      Pinpoint.t -> (allowed * _) t -> Pinpoint.t * (disallowed * allowed) t

    (** The hint for unexplained morphs *)
    val unknown : 'd t

    include Allow_disallow with type (_, _, 'd) sided = 'd t
  end

  module Const : sig
    (** Hints that explain constants. The allowance describes if the constant
        can be on the LHS or RHS of [submode]. *)
    type 'd t constraint 'd = 'l * 'r

    (** The hint for unexplained constants *)
    val unknown : ('l * 'r) t

    (** The hint to explain using [max] on the RHS of [submode]. *)
    val max : (disallowed * 'r) t

    (** The hint to explain using [min] on the LHS of [submode]. *)
    val min : ('l * disallowed) t

    include Allow_disallow with type (_, _, 'd) sided = 'd t
  end
end

module type S = sig
  (** Solver that supports lattices with monotone morphisms between them. *)
  module Solver_mono (Hint : Hint) (C : Lattices_mono) :
    Solver_mono
      with type ('a, 'b, 'd) morph := ('a, 'b, 'd) C.morph
       and type 'a obj := 'a C.obj
       and type pinpoint := Hint.Pinpoint.t
       and type 'd hint_morph := 'd Hint.Morph.t
       and type 'd hint_const := 'd Hint.Const.t
end
