(* TEST
   include stdlib_upstream_compatible;
   expect;
*)

(* Test 1: box on unboxed types - the manifest is stored as Tbox but
   expands to the boxed type during unification *)

type t1 = float# box
type t2 = int# box
type t3 = #(float# * string) box
type 'a t4 = 'a ref# box
[%%expect{|
type t1 = float
type t2 = int
type t3 = float# * string
type 'a t4 = 'a ref
|}]

let g (x : float# box) : float# box = x;;
[%%expect{|
val g : float -> float = <fun>
|}]

(* Test 2: box through type aliases *)

type u = float#
type t = u box;;
let f (x : t) : float = x;;
[%%expect{|
type u = float#
type t = u box
val f : t -> float = <fun>
|}]

type ('a : any) b = 'a box
type t1' = float# b
let check : float -> t1' = fun x -> x
[%%expect{|
type ('a : any) b = 'a box
type t1' = float# b
val check : float -> t1' = <fun>
|}]

let g' (x : float# b) : float# b = x;;
[%%expect{|
val g' : float# b -> float# b = <fun>
|}]

(* Test 3: [float# box] unifies with [float] *)

let h (x : 'a box) (y : 'a) : #(float * float#) = #(x, (y : float#))
[%%expect{|
val h : float -> float# -> #(float * float#) = <fun>
|}]

let i (x : float) : float# box = x;;
[%%expect{|
val i : float -> float = <fun>
|}]

(* Test 4: records too *)

type r = { x : int }
let r_id (rub : r# box) : r = rub;;
[%%expect{|
type r = { x : int; }
val r_id : r -> r = <fun>
|}]

(* but records are still nominally typed *)
type u = #{ x : int }
let bad (ub : u box) : r = ub;;
[%%expect{|
type u = #{ x : int; }
Line 2, characters 27-29:
2 | let bad (ub : u box) : r = ub;;
                               ^^
Error: The value "ub" has type "u box" but an expression was expected of type "r"
       Type "u" is not compatible with type "r#"
|}]

(* and float records don't get unboxed versions *)
type r_float = { x : float }
let r_id (rub : _ box) : r_float = rub;;
[%%expect{|
type r_float = { x : float; }
Line 2, characters 35-38:
2 | let r_id (rub : _ box) : r_float = rub;;
                                       ^^^
Error: The value "rub" has type "'a box" but an expression was expected of type
         "r_float"
|}]

(* Test 5: box types unify with themselves in function types *)

let eq_box (x : int box) (y : int box) = x = y;;
[%%expect{|
val eq_box : int box -> int box -> bool = <fun>
|}]

let eq_float_box (x : float# box) (y : float# box) = x = y;;
[%%expect{|
val eq_float_box : float -> float -> bool = <fun>
|}]

(* Test 6: box in module signatures *)

module type S = sig
  type t = float# box
  val x : t
end;;
[%%expect{|
module type S = sig type t = float val x : t end
|}]

module M : S = struct
  type t = float# box
  let x = 1.0
end;;
[%%expect{|
module M : S
|}]

(* Test 7: Using the module's type *)

let use_m : float = M.x;;
[%%expect{|
val use_m : float = 1.
|}]


(* Test 8: Polymorphic box with explicit jkind annotation *)

let check_boxed_by : type (a : float64). a -> a box -> unit =
  fun _ _ -> ();;
[%%expect{|
val check_boxed_by : ('a : float64). 'a -> 'a box -> unit = <fun>
|}]

type uf = float#
let test_check (u : uf) (f : float) = check_boxed_by u f;;
[%%expect{|
type uf = float#
val test_check : uf -> float -> unit = <fun>
|}]

(* Test 9: Multiple levels of type aliasing - the inner type of box
   must be fully expanded to find the unboxed type *)

type f = float#
type g = f
let test_multi_alias (x : g box) : float = x;;
[%%expect{|
type f = float#
type g = f
val test_multi_alias : g box -> float = <fun>
|}]

type h = g
let test_three_levels (x : h box) : float = x;;
[%%expect{|
type h = g
val test_three_levels : h box -> float = <fun>
|}]

(* Test 10: box on types without unboxed versions *)

type abstract_type
type boxed_abstract = abstract_type box;;
[%%expect{|
type abstract_type
type boxed_abstract = abstract_type box
|}]

(* Test 11: char# box = char *)

type t_char = char# box;;
[%%expect{|
type t_char = char
|}]

let f_char (x : t_char) : char = x;;
[%%expect{|
val f_char : t_char -> char = <fun>
|}]

let g_char (x : char) : char# box = x;;
[%%expect{|
val g_char : char -> char = <fun>
|}]

(* Test 12: Implicit unboxed records - t# box = t
   Mixed records (not float-only) have implicit unboxed versions *)

type mixed_record = { i : int; s : string };;
[%%expect{|
type mixed_record = { i : int; s : string; }
|}]

let mixed_of_unboxed (p : mixed_record# box) : mixed_record = p;;
[%%expect{|
val mixed_of_unboxed : mixed_record -> mixed_record = <fun>
|}]

let unboxed_of_mixed (p : mixed_record) : mixed_record# box = p;;
[%%expect{|
val unboxed_of_mixed : mixed_record -> mixed_record = <fun>
|}]

type umixed = mixed_record#
type boxed_umixed = umixed box;;
[%%expect{|
type umixed = mixed_record#
type boxed_umixed = umixed box
|}]

let convert_alias (x : boxed_umixed) : mixed_record = x;;
[%%expect{|
val convert_alias : boxed_umixed -> mixed_record = <fun>
|}]

(* Float-only records don't have unboxed versions *)
type float_point = { fx : float#; fy : float# };;
[%%expect{|
type float_point = { fx : float#; fy : float#; }
|}]

let float_point_no_unboxed (p : float_point# box) : float_point = p;;
[%%expect{|
val float_point_no_unboxed : float_point -> float_point = <fun>
|}]

(* Test 13: Boxing unboxed tuples *)

type ut = #(int * string);;
[%%expect{|
type ut = #(int * string)
|}]

type boxed_ut = ut box;;
let check : int * string -> boxed_ut = fun x -> x;;
[%%expect{|
type boxed_ut = ut box
val check : int * string -> boxed_ut = <fun>
|}]

let eq_ut (x : #(int * string) box) (y : ut box) = x = y;;
[%%expect{|
val eq_ut : int * string -> ut box -> bool = <fun>
|}]

(* Test 14: Additional jkinds - bits32, bits64, word. The unboxed versions
   of singleton mixed records have these layouts. *)

let check_bits32 : type (a : bits32). a -> a box -> unit =
  fun _ _ -> ();;
[%%expect{|
val check_bits32 : ('a : bits32). 'a -> 'a box -> unit = <fun>
|}]

type r32 = { i32 : int32_u }
let test_bits32 (u : r32#) (b : r32) = check_bits32 u b;;
[%%expect{|
type r32 = { i32 : int32_u; }
val test_bits32 : r32# -> r32 -> unit = <fun>
|}]

let check_bits64 : type (a : bits64). a -> a box -> unit =
  fun _ _ -> ();;
[%%expect{|
val check_bits64 : ('a : bits64). 'a -> 'a box -> unit = <fun>
|}]

type r64 = { i64 : int64_u }
let test_bits64 (u : r64#) (b : r64) = check_bits64 u b;;
[%%expect{|
type r64 = { i64 : int64_u; }
val test_bits64 : r64# -> r64 -> unit = <fun>
|}]

(* [int64] is not the boxed version of [int64_u] *)
let bad_bits64 (u : int64_u) (b : int64) = check_bits64 u b;;
[%%expect{|
Line 1, characters 58-59:
1 | let bad_bits64 (u : int64_u) (b : int64) = check_bits64 u b;;
                                                              ^
Error: The value "b" has type "int64" but an expression was expected of type
         "int64_u box"
|}]

let check_word : type (a : word). a -> a box -> unit =
  fun _ _ -> ();;
[%%expect{|
val check_word : ('a : word). 'a -> 'a box -> unit = <fun>
|}]

type rw = { n : nativeint_u }
let test_word (u : rw#) (b : rw) = check_word u b;;
[%%expect{|
type rw = { n : nativeint_u; }
val test_word : rw# -> rw -> unit = <fun>
|}]

(* Test 15: Parameterized box type alias *)

type ('a : float64) boxed = 'a box;;
[%%expect{|
type ('a : float64) boxed = 'a box
|}]

let use_boxed (x : float# boxed) : float = x;;
[%%expect{|
val use_boxed : float# boxed -> float = <fun>
|}]

type alias_float = float#
let use_boxed_alias (x : alias_float boxed) : float = x;;
[%%expect{|
type alias_float = float#
val use_boxed_alias : alias_float boxed -> float = <fun>
|}]

(* Test 16: Nested box - float# box expands to float, but float is not an
   unboxed version, so float box does NOT expand further to float.
   This tests that box only expands for actual unboxed versions. *)

type nested = float# box box;;
[%%expect{|
type nested = float box
|}]

(* nested = float# box box = float box, which does NOT equal float *)
let nested_to_float (x : nested) : float = x;;
[%%expect{|
Line 1, characters 43-44:
1 | let nested_to_float (x : nested) : float = x;;
                                               ^
Error: The value "x" has type "nested" = "float box"
       but an expression was expected of type "float"
       Type "float" is not compatible with type "float#"
|}]

(* But nested types still unify with each other *)
let nested_eq (x : nested) (y : float# box box) = x = y;;
[%%expect{|
val nested_eq : nested -> float box -> bool = <fun>
|}]

(* Test 17: Type error cases *)

let mismatch1 (x : float# box) : int = x;;
[%%expect{|
Line 1, characters 39-40:
1 | let mismatch1 (x : float# box) : int = x;;
                                           ^
Error: The value "x" has type "float# box" = "float"
       but an expression was expected of type "int"
|}]

let mismatch2 (x : int# box) : int64 = x;;
[%%expect{|
Line 1, characters 39-40:
1 | let mismatch2 (x : int# box) : int64 = x;;
                                           ^
Error: The value "x" has type "int# box" = "int"
       but an expression was expected of type "int64"
|}]

type uf1 = float#
type uf2 = int#
let mismatch3 (x : uf1 box) : uf2 box = x;;
[%%expect{|
type uf1 = float#
type uf2 = int#
Line 3, characters 40-41:
3 | let mismatch3 (x : uf1 box) : uf2 box = x;;
                                            ^
Error: The value "x" has type "uf1 box" = "float"
       but an expression was expected of type "uf2 box" = "int"
|}]

(* Test 18: Type inference *)

let infer1 x = (x : float# box);;
[%%expect{|
val infer1 : float -> float = <fun>
|}]

let infer2 () =
  let f (x : float# box) = x in
  f 1.0;;
[%%expect{|
val infer2 : unit -> float = <fun>
|}]

type ufl = float#
let infer3 (x : ufl box) = x +. 1.0;;
[%%expect{|
type ufl = float#
val infer3 : ufl box -> float = <fun>
|}]

(* Test 19: Distinct vs same underlying box types *)

type t_float_box = float# box
type t_int_box = int# box;;
[%%expect{|
type t_float_box = float
type t_int_box = int
|}]

let distinct1 (x : t_float_box) (y : t_int_box) = x = y;;
[%%expect{|
Line 1, characters 54-55:
1 | let distinct1 (x : t_float_box) (y : t_int_box) = x = y;;
                                                          ^
Error: The value "y" has type "t_int_box" = "int"
       but an expression was expected of type "t_float_box" = "float"
|}]

type ua = float#
type ub = float#
type ta = ua box
type tb = ub box;;
[%%expect{|
type ua = float#
type ub = float#
type ta = ua box
type tb = ub box
|}]

let same_underlying (x : ta) (y : tb) = x = y;;
[%%expect{|
val same_underlying : ta -> tb -> bool = <fun>
|}]

(* Test 20: With constraints in module types *)

module type S_box = sig
  type t
  val x : t
end;;
[%%expect{|
module type S_box = sig type t val x : t end
|}]

module type S_float_box = S_box with type t = float# box;;
[%%expect{|
module type S_float_box = sig type t = float val x : t end
|}]

module M_float_box : S_float_box = struct
  type t = float# box
  let x = 1.0
end;;
[%%expect{|
module M_float_box : S_float_box
|}]

let use_m_float_box : float = M_float_box.x;;
[%%expect{|
val use_m_float_box : float = 1.
|}]

(* Test 21: Functors with box *)

module type UNBOXED = sig
  type t : float64
  val zero : t
end;;
[%%expect{|
module type UNBOXED = sig type t : float64 val zero : t end
|}]

(* Can't yet convert U.t to U.t box generically *)
module type BOXED = sig
  type unboxed : float64
  type t = unboxed box
  (* CR box: once we have the box primitive, change to [val zero : t] *)
  val zero : unboxed
end;;
[%%expect{|
module type BOXED =
  sig type unboxed : float64 type t = unboxed box val zero : unboxed end
|}]

module MakeBoxed (U : UNBOXED) : BOXED with type unboxed = U.t = struct
  type unboxed = U.t
  type t = unboxed box
  let zero = U.zero
end;;
[%%expect{|
module MakeBoxed :
  functor (U : UNBOXED) ->
    sig type unboxed = U.t type t = unboxed box val zero : unboxed end
|}]


(* Test 22: First-class modules *)

module type T_FCM = sig
  type u : any
  type t = u box
  val value : t
end;;
[%%expect{|
module type T_FCM = sig type u : any type t = u box val value : t end
|}]

let fcm : (module T_FCM with type u = float#) =
  (module struct
    type u = float#
    type t = float# box
    let value = 3.14
  end);;
[%%expect{|
val fcm : (module T_FCM with type u = float#) = <module>
|}]

let extract_fcm () =
  let module M = (val fcm) in
  M.value;;
[%%expect{|
val extract_fcm : unit -> float = <fun>
|}]

let fcm_as_float : float = extract_fcm ();;
[%%expect{|
val fcm_as_float : float = 3.14
|}]

(* Test 23: box on value types (int, string)

   Values like int and string are NOT "unboxed versions" of anything,
   but they can still be extra-boxed. *)

type boxed_int = int box;;
[%%expect{|
type boxed_int = int box
|}]

(* int box is not equal to int *)
let int_box_is_int (x : int box) : int = x;;
[%%expect{|
Line 1, characters 41-42:
1 | let int_box_is_int (x : int box) : int = x;;
                                             ^
Error: The value "x" has type "int box" but an expression was expected of type
         "int"
       Type "int" is not compatible with type "int#"
|}]

let int_is_int_box (x : int) : int box = x;;
[%%expect{|
Line 1, characters 41-42:
1 | let int_is_int_box (x : int) : int box = x;;
                                             ^
Error: The value "x" has type "int" but an expression was expected of type
         "int box"
       Type "int#" is not compatible with type "int"
|}]

type boxed_string = string box;;
[%%expect{|
type boxed_string = string box
|}]

(* string box is NOT equal to string *)
let string_box_roundtrip (x : string) : string box = x;;
[%%expect{|
Line 1, characters 53-54:
1 | let string_box_roundtrip (x : string) : string box = x;;
                                                         ^
Error: The value "x" has type "string" but an expression was expected of type
         "string box"
|}]

(* But box types still unify with themselves *)
let int_box_eq (x : int box) (y : int box) = x = y;;
[%%expect{|
val int_box_eq : int box -> int box -> bool = <fun>
|}]

(* Test 24: Recursive types with box *)

type ('a : any) box_tree = Leaf | Node of 'a box * 'a box_tree * 'a box_tree

type float_tree = float# box_tree;;
[%%expect{|
type ('a : any) box_tree = Leaf | Node of 'a box * 'a box_tree * 'a box_tree
type float_tree = float# box_tree
|}]

let make_float_tree () : float_tree =
  Node (1.0, Node (2.0, Leaf, Leaf), Leaf);;
[%%expect{|
val make_float_tree : unit -> float_tree = <fun>
|}]

let sum_tree t =
  let rec go acc = function
    | Leaf -> acc
    | Node (x, l, r) -> go (go (acc +. x) l) r
  in go 0.0 t;;
[%%expect{|
val sum_tree : float# box_tree -> float = <fun>
|}]

let sum_float_tree (t : float_tree) = sum_tree t;;
[%%expect{|
val sum_float_tree : float_tree -> float = <fun>
|}]

(* Test 25: Polymorphic box unification with concrete boxed types *)

let f_box : 'a box -> 'a box = fun x -> x;;
[%%expect{|
val f_box : ('a : any). 'a box -> 'a box = <fun>
|}]

let _ = f_box 5.;;
[%%expect{|
- : float = 5.
|}]

let _ = f_box 'x';;
[%%expect{|
- : char = 'x'
|}]

(* int also has an unboxed version (int#) *)
let _ = f_box 42;;
[%%expect{|
- : int = 42
|}]

(* Test that this does NOT work for types without unboxed versions *)
let _ = f_box "hello";;
[%%expect{|
Line 1, characters 14-21:
1 | let _ = f_box "hello";;
                  ^^^^^^^
Error: This constant has type "string" but an expression was expected of type
         "'a box"
|}]

(* ... including int32, whose unboxed version was removed *)
let _ = f_box 42l;;
[%%expect{|
Line 1, characters 14-17:
1 | let _ = f_box 42l;;
                  ^^^
Error: The constant "42l" has type "int32" but an expression was expected of type
         "'a box"
|}]

(* Test 26: Subtyping with polymorphic variants and box *)

type ab = [ `A | `B ]
type a  = [ `A ];;
[%%expect{|
type ab = [ `A | `B ]
type a = [ `A ]
|}]

let coerce_box (x : a box) : ab box = (x :> ab box);;
[%%expect{|
val coerce_box : a box -> ab box = <fun>
|}]

(* Also test the other direction fails *)
let coerce_box_fail (x : ab box) : a box = (x :> a box);;
[%%expect{|
Line 1, characters 43-55:
1 | let coerce_box_fail (x : ab box) : a box = (x :> a box);;
                                               ^^^^^^^^^^^^
Error: Type "ab box" = "[ `A | `B ] box" is not a subtype of "a box" = "[ `A ] box"
       Type "ab" = "[ `A | `B ]" is not a subtype of "a" = "[ `A ]"
       The second variant type does not allow tag(s) "`B"
|}]

(* [float# box] unifies with [float] across a subtype coercion *)
let coerce_unbox (x : [ `A of float# box ]) =
  (x :> [ `A of float | `B ]);;
[%%expect{|
val coerce_unbox : [ `A of float ] -> [ `A of float | `B ] = <fun>
|}]

(* Test 27: Recursive type declarations with box
   These test well-foundedness and infinite size checks *)

type a_rec = { a_rec : a_rec box } [@@unboxed];;
[%%expect{|
type a_rec = { a_rec : a_rec box; } [@@unboxed]
|}]

type t1_rec = { t2_rec : t2_rec box } [@@unboxed]
and t2_rec = t1_rec box;;
[%%expect{|
type t1_rec = { t2_rec : t2_rec box; } [@@unboxed]
and t2_rec = t1_rec box
|}]

(* Recursive types with box that are NOT unboxed also work *)
type b_rec = { b_rec_field : b_rec box };;
[%%expect{|
type b_rec = { b_rec_field : b_rec box; }
|}]

(* Mutually recursive with box *)
type c1 = { c2_field : c2 box }
and c2 = { c1_field : c1 box };;
[%%expect{|
type c1 = { c2_field : c2 box; }
and c2 = { c1_field : c1 box; }
|}]


(* Test 28: [any] in [box] is representable *)

type t : any
type foo = t box
let f (foo : foo) = foo

[%%expect{|
type t : any
type foo = t box
val f : foo -> foo = <fun>
|}]


(* Test 29: GADTs *)

let require_boxed (_ : _ box) = ()
type 'a st = T : string st
type 'a ft = T : float ft
type 'a bt = T : _ box bt
[%%expect{|
val require_boxed : ('a : any). 'a box -> unit = <fun>
type 'a st = T : string st
type 'a ft = T : float ft
type 'a bt = T : 'b box bt
|}]

(* We can match on a GADT to learn that something is boxed *)
let ok_bt (type a) (a : a) (x : a bt) =
  match x with
  | T -> require_boxed a
[%%expect{|
val ok_bt : 'a -> 'a bt -> unit = <fun>
|}]

let ok_ft (type a) (a : a) (x : a ft) =
  match x with
  | T -> require_boxed a
[%%expect{|
val ok_ft : 'a -> 'a ft -> unit = <fun>
|}]

let bad_st (type a) (a : a) (x : a st) =
  match x with
  | T -> require_boxed a
[%%expect{|
Line 3, characters 23-24:
3 |   | T -> require_boxed a
                           ^
Error: The value "a" has type "a" = "string" but an expression was expected of type
         "'a box"
|}]

(* We can't discover something is boxed, then lower the GADT type (this would be
   unsound) *)

let bad (x : 'a st) (a : 'a) =
  require_boxed a;
  match x with
  | T ->
    ()
[%%expect{|
Line 4, characters 4-5:
4 |   | T ->
        ^
Error: This pattern matches values of type "string st"
       but a pattern was expected which matches values of type "$0 box st"
       Type "string" is not compatible with type "$0 box"
       The type constructor "$0" would escape its scope
|}]

(* Test 30: [(T box)#] = [T] -- the unboxed version of a [_ box] alias is
   the inner type. *)

type t = int box
type u = t#
let u_is_int (x : u) : int = x
let int_is_u (x : int) : u = x
[%%expect{|
type t = int box
type u = t#
val u_is_int : u -> int = <fun>
val int_is_u : int -> u = <fun>
|}]

(* The same property holds parametrically. *)

type ('a : any) my_box = 'a box
type 'a uu = 'a my_box#
let probe (x : 'a uu) : 'a = x
let probe_inv (x : 'a) : 'a uu = x
[%%expect{|
type ('a : any) my_box = 'a box
type 'a uu = 'a my_box#
val probe : 'a uu -> 'a = <fun>
val probe_inv : 'a -> 'a uu = <fun>
|}]

(* Test 31: [box] on object types *)

type obj = < m : int >
type t_obj_box = obj box;;
[%%expect{|
type obj = < m : int >
type t_obj_box = obj box
|}]

let obj_box_eq (x : obj box) (y : obj box) = x = y;;
[%%expect{|
val obj_box_eq : obj box -> obj box -> bool = <fun>
|}]

(* Object subtyping is preserved through [box] *)
type obj_more = < m : int; n : int >
let widen (x : obj_more box) = (x :> obj box);;
[%%expect{|
type obj_more = < m : int; n : int >
val widen : obj_more box -> obj box = <fun>
|}]

(* Test 32: Box creates an unboxed version *)

type t = string box
type u = t#
let check : u -> string = fun x -> x
[%%expect{|
type t = string box
type u = t#
val check : u -> string = <fun>
|}]

type 'a t = 'a box
type 'a u = 'a t#
let check : 'a -> 'a u = fun x -> x
[%%expect{|
type 'a t = 'a box
type 'a u = 'a t#
val check : 'a -> 'a u = <fun>
|}]

type i = int t#
let check : i -> int = fun x -> x
[%%expect{|
type i = int t#
val check : i -> int = <fun>
|}]

module M : sig
  type ('a : any) b = 'a box
  type ('a : any) t = 'a
  type ('a : any) t2 = 'a
  type s = string
  type tup = int * int
end = struct
  type ('a : any) b = 'a box
  type ('a : any) t = 'a b#
  type ('a : any) t2 = 'a box#
  type s = string b#
  type tup = #(int * int) b
end
[%%expect{|
module M :
  sig
    type ('a : any) b = 'a box
    type ('a : any) t = 'a
    type ('a : any) t2 = 'a
    type s = string
    type tup = int * int
  end
|}]

module M : sig
  type ('a : any) b = 'a box
  type ('a : any) t = 'a b#
  type ('a : any) t2 = 'a box#
  type s = string b#
  type tup = #(int * int) b
end = struct
  type ('a : any) b = 'a box
  type ('a : any) t = 'a
  type ('a : any) t2 = 'a
  type s = string
  type tup = int * int
end
[%%expect{|
module M :
  sig
    type ('a : any) b = 'a box
    type ('a : any) t = 'a b#
    type ('a : any) t2 = 'a box#
    type s = string b#
    type tup = #(int * int) b
  end
|}]

(* Test 33: multiple layers of [box] *)

type int_b = int box
type int_b_b = int_b box
type int_b_b_u = int_b_b#
let check : int_b_b_u -> int_b = fun x -> x
(* CR box rtjoa: Unboxing is approximate. This should typecheck *)
type int_b_b_u_u = int_b_b_u#
let check : int_b_b_u_u -> int = fun x -> x
type int_b_b_u_u_u = int_b_b_u_u#
let check : int_b_b_u_u_u -> int# = fun x -> x
[%%expect{|
type int_b = int box
type int_b_b = int_b box
type int_b_b_u = int_b_b#
val check : int_b_b_u -> int_b = <fun>
Line 6, characters 19-29:
6 | type int_b_b_u_u = int_b_b_u#
                       ^^^^^^^^^^
Error: The type "int_b_b_u" has no unboxed version.
|}]

module M : sig
  type t
  type t_box_unbox_box_unbox = t
end = struct
  type t
  type t_box = t box
  type t_box_unbox = t_box#
  type t_box_unbox_box = t_box_unbox box
  type t_box_unbox_box_unbox = t_box_unbox_box#
end
[%%expect{|
module M : sig type t type t_box_unbox_box_unbox = t end
|}]

(* Test 34: shadowing the predef [box] disambiguates with [box/2] *)

module Shadowing = struct
  let id_box (x : 'a box) : 'a box = x
  type 'a box = Mine of 'a
  let still_id = id_box
end
[%%expect{|
module Shadowing :
  sig
    val id_box : ('a : any). 'a box -> 'a box
    type 'a box = Mine of 'a
    val still_id : ('a : any). 'a box/2 -> 'a box/2
  end
|}]

(* Test 35: type compatibility *)

module Inst_value : sig
  type t
end = struct
  type t
end
[%%expect{|
module Inst_value : sig type t end
|}]

module NonInst_value : sig
  type _ t
end = struct
  type _ t
end
[%%expect{|
module NonInst_value : sig type _ t end
|}]

module Inst_value_value : sig
  type t : value & value
end = struct
  type t : value & value
end
[%%expect{|
module Inst_value_value : sig type t : value & value end
|}]

module NonInst_value_value : sig
  type _ t : value & value
end = struct
  type _ t : value & value
end
[%%expect{|
module NonInst_value_value : sig type _ t : value & value end
|}]

module Inst_value2 : sig
  type t
end = struct
  type t
end
[%%expect{|
module Inst_value2 : sig type t end
|}]

module Inst_untagged_immediate : sig
  type t : untagged_immediate
end = struct
  type t : untagged_immediate
end
[%%expect{|
module Inst_untagged_immediate : sig type t : untagged_immediate end
|}]

module Inst_bits64 : sig
  type t : bits64
end = struct
  type t = int64_u
end
[%%expect{|
module Inst_bits64 : sig type t : bits64 end
|}]

module Comp = struct
  type _ t  = |
  type _ t' = |
end

(* Comparing box with other type constructors *)

(* A boxed aliasable type is compatible with a type with an unboxed version
   (thus [eq] can't be refuted) *)
let f (eq : (Inst_untagged_immediate.t box, int) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
module Comp : sig type _ t = | type _ t' = | end
Line 11, characters 16-17:
11 |   match eq with _ -> .
                     ^
Error: This match case could not be refuted.
       Here is an example of a value that would reach it: "Equal"
|}]

(* as well as *aliasable* type that could be hiding an unboxed version.
   (Techically, this exact test case is conservative, as a type cannot be the
   unboxed version of itself due to the cyclic type check) *)
let f (eq : (Inst_value.t box, Inst_value.t) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
Line 2, characters 16-17:
2 |   match eq with _ -> .
                    ^
Error: This match case could not be refuted.
       Here is an example of a value that would reach it: "Equal"
|}]

(* We can refute the below, as we can see that the unboxed version of [int]
   isn't [value] *)
let f (eq : (Inst_value.t box, int) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : (Inst_value.t box, int) Type.eq -> unit = <fun>
|}]

(* Boxed types are also incompatible with un-aliasable type without unboxed
   versions *)
let f (eq : (Inst_value.t box, string) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : (Inst_value.t box, string) Type.eq -> unit = <fun>
|}]

(* ['a box] and ['b box] are compatible if ['a] and ['b] are *)

let f (eq : (Inst_value.t box, Inst_value2.t box) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
Line 2, characters 16-17:
2 |   match eq with _ -> .
                    ^
Error: This match case could not be refuted.
       Here is an example of a value that would reach it: "Equal"
|}]

let f (eq : (int box, string box) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : (int box, string box) Type.eq -> unit = <fun>
|}]


let f (eq : (Inst_value.t box, Inst_bits64.t box) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : (Inst_value.t box, Inst_bits64.t box) Type.eq -> unit = <fun>
|}]

(* ['a box] is compatible with tuples *)

let f (eq : (Inst_value_value.t box, int * int) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
Line 2, characters 16-17:
2 |   match eq with _ -> .
                    ^
Error: This match case could not be refuted.
       Here is an example of a value that would reach it: "Equal"
|}]
let f (eq : (_ NonInst_value_value.t box, int * int) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
Line 2, characters 16-17:
2 |   match eq with _ -> .
                    ^
Error: This match case could not be refuted.
       Here is an example of a value that would reach it: "Equal"
|}]
let f (eq : (_ NonInst_value_value.t box Comp.t, (int * int) Comp.t') Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
Line 2, characters 16-17:
2 |   match eq with _ -> .
                    ^
Error: This match case could not be refuted.
       Here is an example of a value that would reach it: "Equal"
|}]

(* We refute this one because #(int * int) is not [value] *)
let f (eq : (Inst_value.t box, int * int) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : (Inst_value.t box, int * int) Type.eq -> unit = <fun>
|}]
let f (eq : (_ NonInst_value.t box, int * int) Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : ('a NonInst_value.t box, int * int) Type.eq -> unit = <fun>
|}]
let f (eq : (_ NonInst_value.t box Comp.t, (int * int) Comp.t') Type.eq) : unit =
  match eq with _ -> .
[%%expect{|
val f : ('a NonInst_value.t box Comp.t, (int * int) Comp.t') Type.eq -> unit =
  <fun>
|}]

(* Test 36: preliminary testing of boxing primitives *)

(* Incorrect versions of boxing primitives, to test typechecking. *)

(* float *)
open (struct
  external box : float# -> float = "%box_float"
  external unbox : float -> float# = "%unbox_float"
  let box = Obj.magic box
  let unbox = Obj.magic unbox
end : sig
  val box : ('a : any). 'a -> 'a box
  val unbox : ('a : any). 'a box -> 'a
end)
[%%expect{|
val box : ('a : any). 'a -> 'a box = <fun>
val unbox : ('a : any). 'a box -> 'a = <fun>
|}]

let box_float : float# -> float = box
let unbox_float : float -> float# = unbox
let float_0_via_box = box #0.
let float_0_unbox = unbox 0.
[%%expect{|
val box_float : float# -> float = <fun>
val unbox_float : float -> float# = <fun>
val float_0_via_box : float = 0.
val float_0_unbox : float# = <abstr>
|}]

(* ref *)
open (struct
  let box contents = { contents }
  let unbox { contents } = contents
  let box = Obj.magic box
  let unbox = Obj.magic unbox
end : sig
  val box : ('a : any). 'a -> 'a box
  val unbox : ('a : any). 'a box -> 'a
end)
[%%expect{|
val box : ('a : any). 'a -> 'a box = <fun>
val unbox : ('a : any). 'a box -> 'a = <fun>
|}]

let box_ref : 'a ref# -> 'a ref = box
let unbox_ref : 'a ref -> 'a ref# = unbox
let ref_0_via_box = box #{ contents = 0 }
let ref_0_unbox = unbox { contents = 0 }
[%%expect{|
val box_ref : 'a ref# -> 'a ref = <fun>
val unbox_ref : 'a ref -> 'a ref# = <fun>
val ref_0_via_box : int ref = {contents = 0}
val ref_0_unbox : int ref# = #{contents = 0}
|}]

(* Test 37: Private type abbreviations *)

type t = private int box
(* The implied unboxed definition is [type t# = private int] *)
[%%expect{|
type t = private int box
|}]

(* hence [t# =/= int] *)
let id (x : t#) = (x : int)
[%%expect{|
Line 1, characters 19-20:
1 | let id (x : t#) = (x : int)
                       ^
Error: The value "x" has type "t#" but an expression was expected of type "int"
|}]

(* but [t#] is coercible to [int] *)
let id (x : t#) = (x :> int)
[%%expect{|
val id : t# -> int = <fun>
|}]

(* Test 38: Unboxing through abbreviations *)

(* CR box rtjoa: Unboxing is approximate. This should typecheck *)
type dummy
type ('a, 'b) box' = 'b box
type a = (dummy, (dummy, int) box') box'
type b = a#
type c = b#
[%%expect{|
type dummy
type ('a, 'b) box' = 'b box
type a = (dummy, (dummy, int) box') box'
type b = a#
Line 5, characters 9-11:
5 | type c = b#
             ^^
Error: The type "b" has no unboxed version.
|}]

(* ... *)

type 'x id = 'x

type 'x s1 = 'x id box
type 'x s2 = 'x s1 box
[%%expect{|
type 'x id = 'x
type 'x s1 = 'x id box
type 'x s2 = 'x s1 box
|}]

type ('a, 'b) t = ('a * 'b) s1
type ('a, 'b) t' = ('a, 'b) t#

let id (x : (int, string) t') : int * string = x
[%%expect{|
type ('a, 'b) t = ('a * 'b) s1
type ('a, 'b) t' = ('a, 'b) t#
val id : (int, string) t' -> int * string = <fun>
|}]

type ('a, 'b) t'' = ('a, 'b) t'#
[%%expect{|
Line 1, characters 29-32:
1 | type ('a, 'b) t'' = ('a, 'b) t'#
                                 ^^^
Error: The type "t'" has no unboxed version.
|}]

type ('a, 'b) t = ('a * 'b) s2
type ('a, 'b) t' = ('a, 'b) t#
type ('a, 'b) t'' = ('a, 'b) t'#

(* CR box rtjoa: this should typecheck, but unboxing is approximate *)
let id (x : (int, string) t'') : int * string = x
[%%expect{|
type ('a, 'b) t = ('a * 'b) s2
type ('a, 'b) t' = ('a, 'b) t#
Line 3, characters 29-32:
3 | type ('a, 'b) t'' = ('a, 'b) t'#
                                 ^^^
Error: The type "t'" has no unboxed version.
|}]

(* CR box rtjoa: this should typecheck, but unboxing is approximate *)
type ('a, 'b) t'' = ('a, 'b) t'#
[%%expect{|
Line 1, characters 29-32:
1 | type ('a, 'b) t'' = ('a, 'b) t'#
                                 ^^^
Error: The type "t'" has no unboxed version.
|}]

type ('a, 'b) t'' = ('a, 'b) t''#
[%%expect{|
Line 1, characters 0-33:
1 | type ('a, 'b) t'' = ('a, 'b) t''#
    ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
Error: The type "t''" has no unboxed version.
|}]

(* Test 38: Unboxing through tuple type abbreviation *)

type ('a, 'b) t = 'a * 'b
type 'a t' = ('a, 'a) t#

let id (x : int t') : #(int * int) = x
[%%expect{|
type ('a, 'b) t = 'a * 'b
type 'a t' = ('a, 'a) t#
val id : int t' -> #(int * int) = <fun>
|}]

(* Test 39: Cycles *)

type s = t box
and t = s#
[%%expect{|
Line 1, characters 0-14:
1 | type s = t box
    ^^^^^^^^^^^^^^
Error: The definition of "s" contains a cycle:
         "s" = "t box",
         "t box" = "t box",
         "t box" contains "t",
         "t" = "s#",
         "s#" = "t box#",
         "t box#" = "t",
         "t" = "s#"
|}]

(* CR box jbachurski: This should probably complain about the cycle instead
   once we expand abbreviations when determining unboxed versions. *)
type s1 = t2 box
and s2 = s1 box
and t1 = s2#
and t2 = t1#
[%%expect{|
Line 4, characters 0-12:
4 | and t2 = t1#
    ^^^^^^^^^^^^
Error: The type "t1" has no unboxed version.
|}]

(* Test 40: GADT equations commute *)

(* Typechecking for [box] does not depend on the order in which GADT equations
   are introduced: learning [a = au box] and [a = float] (hence [au = float#])
   typechecks regardless of which equation comes first. *)

type (_, _ : any) boxes = Boxes : ('a : any). ('a box, 'a) boxes
[%%expect{|
type (_, _ : any) boxes = Boxes : ('a : any). ('a box, 'a) boxes
|}]

(* Matching [Boxes] before [Equal] *)
let f (type a) (type (au : float64))
      (Boxes : (a, au) boxes) (Equal : (a, float) Type.eq)
      (au : au) : au =
  let z : a# = au in z
[%%expect{|
val f :
  'a ('au : float64). ('a, 'au) boxes -> ('a, float) Type.eq -> 'au -> 'au =
  <fun>
|}]

(* Matching [Equal] before [Boxes] *)
let f (type a) (type (au : float64))
      (Equal : (a, float) Type.eq) (Boxes : (a, au) boxes)
      (au : au) : au =
  let z : a# = au in z
[%%expect{|
val f :
  'a ('au : float64). ('a, float) Type.eq -> ('a, 'au) boxes -> 'au -> 'au =
  <fun>
|}]

(* Same examples, using intermediate modules *)

(* Matching [Boxes] before [Equal] *)
let f (type a) (type (au : float64))
      (eq : (a, float) Type.eq) (boxes : (a, au) boxes) (au : au) : au =
  let Boxes = boxes in
  let Equal = eq in
  let module M = struct
    type b = a
    let z : b# = au
  end in
  M.z
[%%expect{|
val f :
  'a ('au : float64). ('a, float) Type.eq -> ('a, 'au) boxes -> 'au -> 'au =
  <fun>
|}]

(* Matching [Equal] before [Boxes] *)
let f (type a) (type (au : float64))
      (eq : (a, float) Type.eq) (boxes : (a, au) boxes) (au : au) : au =
  let Equal = eq in
  let Boxes = boxes in
  let module M = struct
    type b = a
    let z : b# = au
  end in
  M.z
[%%expect{|
val f :
  'a ('au : float64). ('a, float) Type.eq -> ('a, 'au) boxes -> 'au -> 'au =
  <fun>
|}]

(* Test 41: GADTs refine unboxed versions *)

(* We can learn that types have an unboxed version from GADT equations *)

module M : sig
  type t
  type r
  type r_box
end = struct
  type nonrec t = float
  type nonrec r = r
  type r_box = r box
end

(* The lookup for [M.t#] cannot succeed until we match on [Equal] *)
let f (Equal : (M.t, float) Type.eq) (x : float#) = (x : M.t#)
[%%expect{|
module M : sig type t type r type r_box end
val f : (M.t, float) Type.eq -> float# -> M.t# = <fun>
|}]

let f (Equal : (M.r, r) Type.eq) (x : r#) = (x : M.r#)
[%%expect{|
val f : (M.r, r) Type.eq -> r# -> M.r# = <fun>
|}]

let f (Equal : (M.r_box, r box) Type.eq) (x : r) = (x : M.r_box#)
[%%expect{|
val f : (M.r_box, r box) Type.eq -> r -> M.r_box# = <fun>
|}]

(* Introducing unboxed versions with a GADT equation *)

(* CR box jbachurski: I think it's doable to support these, but requires
   some modifications in [add_gadt_equation]. *)

let f (Equal : (M.t#, float#) Type.eq) (x : float#) = (x : M.t#)
[%%expect{|
Line 1, characters 16-20:
1 | let f (Equal : (M.t#, float#) Type.eq) (x : float#) = (x : M.t#)
                    ^^^^
Error: The type "M.t" has no unboxed version.
|}]

let f (Equal : (M.t#, float#) Type.eq) (x : float) = (x : M.t)
[%%expect{|
Line 1, characters 16-20:
1 | let f (Equal : (M.t#, float#) Type.eq) (x : float) = (x : M.t)
                    ^^^^
Error: The type "M.t" has no unboxed version.
|}]

let f (Equal : (M.t#, string) Type.eq) (x : string box) = (x : M.t)
[%%expect{|
Line 1, characters 16-20:
1 | let f (Equal : (M.t#, string) Type.eq) (x : string box) = (x : M.t)
                    ^^^^
Error: The type "M.t" has no unboxed version.
|}]

(* Test 42: Subsumption checks with [box] *)

(* [box] is total *)
module M : sig
  val f : 'a box -> 'c
end = struct
  let f (x : 'b) : 'c = Obj.magic x
end
[%%expect{|
module M : sig val f : 'a box -> 'c end
|}]

(* [box] is not surjective *)
module M : sig
  val f : 'b -> 'c
end = struct
  let f (x : 'a box) : 'c = Obj.magic x
end
[%%expect{|
Lines 3-5, characters 6-3:
3 | ......struct
4 |   let f (x : 'a box) : 'c = Obj.magic x
5 | end
Error: Signature mismatch:
       Modules do not match:
         sig val f : ('a : any) 'c. 'a box -> 'c end
       is not included in
         sig val f : 'b -> 'c end
       Values do not match:
         val f : ('a : any) 'c. 'a box -> 'c
       is not included in
         val f : 'b -> 'c
       The type "'a box -> 'b" is not compatible with the type "'c -> 'd"
       Type "'a box" is not compatible with type "'c"
|}]

(* [box] is covariant *)
module M : sig
  type +'a t
end = struct
  type 'a t = 'a box
end
[%%expect{|
module M : sig type +'a t end
|}]

(* [box] can reduce [moregen]s via unboxing - [int * string < 'a box] *)
module M : sig
  val unbox : int * string -> #(int * string)
end = struct
  let unbox (x : 'a box) : 'a = Obj.magic Obj.magic x
end
[%%expect{|
module M : sig val unbox : int * string -> #(int * string) end
|}]

(* same test, simpler unification order - [#(int * string) < a] happens first *)
module M : sig
  val unbox : #(int * string) -> int * string
end = struct
  let unbox (x : 'a) : 'a box = Obj.magic Obj.magic x
end
[%%expect{|
module M : sig val unbox : #(int * string) -> int * string end
|}]

(* Test 43: Type equality checks with [box] *)

(* trivial *)
module M : sig
  type 'a t = 'a box
end = struct
  type 'a t = 'a box
end
[%%expect{|
module M : sig type 'a t = 'a box end
|}]

(* reductions still happen *)
module M : sig
  type t = float
end = struct
  type t = float# box
end

[%%expect{|
module M : sig type t = float end
|}]

(* Test 44: Signature avoidance can cause errors at functor application *)

module type S = sig
  type t
end
module F(X : S) = struct
  type x = X.t
  type y = X.t box
  type yu = y#
end

module M = F(struct
  type t
end)
[%%expect{|
module type S = sig type t end
module F :
  functor (X : S) -> sig type x = X.t type y = X.t box type yu = y# end
Lines 10-12, characters 11-4:
10 | ...........F(struct
11 |   type t
12 | end)
Error: In the signature of this functor application: The type "y"
       has no unboxed version.
|}]
