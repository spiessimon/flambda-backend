(* TEST
 flags += "-extension mode_polymorphism_alpha -extension mode_polymorphism_printing";
 expect;
*)

let use_uncontended (x @ uncontended) = ()
let use_portable (x @ portable) = ()
let use_unique (x @ unique) = ()
let use_static (x @ static) = ()
let use_global (x @ global) = ()
[%%expect{|
val use_uncontended : 'a @ [< uncontended] -> unit @ 'm = <fun>
val use_portable : 'a @ [< portable] -> unit @ 'm = <fun>
val use_unique : 'a @ [< unique] -> unit @ 'm = <fun>
val use_static : 'a @ [< static] -> unit @ 'm = <fun>
val use_global : 'a @ [< global] -> unit @ 'm = <fun>
|}]

(* BASIC POLYMORPHISM *)

let foo =
  let foo x = x in
  let x = ref 42 in
  let x = foo x in
  let (y @ contended) = ref !x in
  let _ = foo y in
  foo
[%%expect{|
val foo : '_weak1 -> '_weak1 @ [> aliased stateful dynamic] = <fun>
|}]

let id x = x
[%%expect{|
val id : 'a @ [< 'm] -> 'a @ [> 'm] = <fun>
|}]

let () =
  let x = ref 42 in
  let x = id x in
  let (y @ contended) = ref !x in
  let _ = id y in
  ()
[%%expect{|
|}]

(* instantiation [id] does not make it less polymorphic *)
let foo (x @ portable) = id x
let id' = id
[%%expect{|
val foo : 'a @ [< 'm & global portable] -> 'a @ [> 'm | dynamic] = <fun>
val id' : 'a @ [< 'm] -> 'a @ [> 'm] = <fun>
|}]

let foo (x @ nonportable) =
  let x = id' x in
  use_portable x
[%%expect{|
Line 3, characters 15-16:
3 |   use_portable x
                   ^
Error: This value is "nonportable" but is expected to be "portable".
|}]

let bar (c @ unique) =
  use_unique (id c)
[%%expect{|
val bar : 'a @ [< global unique] -> unit @ [> dynamic] = <fun>
|}]

let bar (c @ local) =
  let _ = use_unique (id c) in
  ()
[%%expect{|
val bar : 'a @ [< unique > local] -> unit @ 'm = <fun>
|}]

let bar (x @ aliased) =
  let x = id x in
  let _ = use_unique x in
  ()
[%%expect{|
Line 3, characters 21-22:
3 |   let _ = use_unique x in
                         ^
Error: This value is "aliased" but is expected to be "unique".
|}]

let bar (x @ many) =
  let y @ once = x in
  let y = id y in
  (y, y)
[%%expect{|
Line 4, characters 6-7:
4 |   (y, y)
          ^
Error: This value is used here,
       but it is defined as once and is also being used at:
Line 4, characters 3-4:
4 |   (y, y)
       ^

|}]

(* the result of a mode polymorphic function can't be static *)
let foo (x @ static) =
  let x = id x in
  use_static x
[%%expect{|
Line 3, characters 13-14:
3 |   use_static x
                 ^
Error: This value is "dynamic"
         because function applications are always dynamic.
       However, the highlighted expression is expected to be "static".
|}]

(* mode polymorphism allows us to combine take combine the bounds of two
functions via joins *)
let f (x : string) = x
let g (x : string @ portable) = x
let which = function
  | false -> f
  | true -> g
[%%expect{|
val f :
  string @ [< 'm mod contended immutable] ->
  string @ [> 'm mod many portable forkable unyielding stateless] = <fun>
val g :
  string @ [< 'm mod contended immutable & portable] ->
  string @ [> 'm mod many portable forkable unyielding stateless] = <fun>
val which :
  bool @ 'n ->
  string @ [< 'm mod contended immutable & portable] ->
  string @ [> 'm mod many portable forkable unyielding stateless] = <fun>
|}]

(* The least upper bound between portable and nonportable is nonportable *)
let foo (x @ portable) =
  let f = which true in
  use_portable (f x) (* x is weakened to nonportable before it's applied to f *)
[%%expect{|
val foo : string @ [< global portable] -> unit @ [> dynamic] = <fun>
|}]

let foo (x @ portable) =
  let f = which false in
  use_global (f x) (* x is weakened to nonportable before it's applied to f *)
[%%expect{|
val foo : string @ [< global portable] -> unit @ [> dynamic] = <fun>
|}]

(* mode variables used at some mode imposes a bound on them *)
let id x = use_portable x; x
[%%expect{|
val id : 'a @ [< 'm & many portable] -> 'a @ [> 'm | aliased] = <fun>
|}]

let foo (x @ nonportable) =
  id x
[%%expect{|
Line 2, characters 5-6:
2 |   id x
         ^
Error: This value is "nonportable" but is expected to be "portable".
|}]

(* but they remain polymorphic on the other axes *)

let foo (x @ contended) (y @ uncontended) =
  let x = id x in
  let y = id y in
  use_uncontended y; (* this use succeeds *)
  use_uncontended x (* this use fails *)
[%%expect{|
Line 5, characters 18-19:
5 |   use_uncontended x (* this use fails *)
                      ^
Error: This value is "contended" but is expected to be "uncontended".
|}]

(* CLOSING OVER MODE VARIABLES *)
(* Basic closing-over behavior. See [currying.ml] for more intricate patterns *)

let close_over x = fun () -> x
[%%expect{|
val close_over : 'a @ [< 'm & global] -> unit @ 'n -> 'a @ [> 'm] = <fun>
|}]

let foo (x @ portable) (y @ nonportable) =
  let const_x = close_over x in
  let const_y = close_over y in
  use_portable (const_x ());
  use_portable (const_y ())
[%%expect{|
Line 5, characters 15-27:
5 |   use_portable (const_y ())
                   ^^^^^^^^^^^^
Error: This value is "nonportable" but is expected to be "portable".
|}]

let close_over x = fun () -> fun () -> x
[%%expect{|
val close_over :
  'a @ [< 'm & global] -> unit @ 'o -> unit @ 'n -> 'a @ [> 'm] = <fun>
|}]

let foo (x @ portable) (y @ nonportable) =
  let const_x = close_over x in
  let const_y = close_over y in
  use_portable (const_x () ());
  use_portable (const_y () ())
[%%expect{|
Line 5, characters 15-30:
5 |   use_portable (const_y () ())
                   ^^^^^^^^^^^^^^^
Error: This value is "nonportable" but is expected to be "portable".
|}]

(* partially applying a nested closure similarly depends on input mode *)
let foo (x @ portable) =
  let const_x = close_over x in
  use_portable (const_x ())
[%%expect{|
val foo : 'a @ [< global portable] -> unit @ [> dynamic] = <fun>
|}]

let foo (x @ portable) =
  use_portable (close_over x)
[%%expect{|
val foo : 'a @ [< global portable] -> unit @ [> dynamic] = <fun>
|}]

(* MODE CROSSING *)

(* mode crossing is stronger than mode polymorphism *)
let foo (x : int @ portable) (y : int @ nonportable) =
  let x = id x in
  let y = id y in
  use_portable x;
  use_portable y
[%%expect{|
val foo : int @ [< portable] -> int @ [> nonportable] -> unit @ [> dynamic] =
  <fun>
|}, Principal{|
val foo :
  int @ [< global portable] -> int @ [> nonportable] -> unit @ [> dynamic] =
  <fun>
|}]

(* LOCAL AND MODE POLYMORPHISM *)

(* a function whose return does not allocate can be polymorphic over locality *)
let id x = x

let foo (x @ local) =
  let x = id x in
  use_global x
[%%expect{|
val id : 'a @ [< 'm] -> 'a @ [> 'm] = <fun>
Line 5, characters 13-14:
5 |   use_global x
                 ^
Error: This value is "local" but is expected to be "global".
|}]

(* if the return value is allocated, its locality is restricted to global *)
let some x = Some x

let foo (x @ local) =
  let x = some x in
  ()
[%%expect{|
val some : 'a @ [< 'm & global] -> 'a option @ [> 'm] = <fun>
Line 4, characters 15-16:
4 |   let x = some x in
                   ^
Error: This value is "local" to the parent region but is expected to be "global".
|}]

(* unless there is an exclave_ *)
let some x = exclave_ (Some x)

let foo (x @ local) =
  let x = some x in
  use_global x
[%%expect{|
val some : 'a @ [< 'm] -> 'a option @ [> 'm | local] = <fun>
Line 5, characters 13-14:
5 |   use_global x
                 ^
Error: This value is "local" but is expected to be "global".
|}]

(* local values stay local through id_local - can't return without exclave_ *)
let id_local (x @ local) = x

let foo (x @ local) =
  let y = id_local x in
  y
[%%expect{|
val id_local : 'a @ [< 'm > local] -> 'a @ [> 'm | local] = <fun>
Line 5, characters 2-3:
5 |   y
      ^
Error: This value is "local"
       but is expected to be "local" to the parent region or "global"
         because it is a function return value.
         Hint: Use exclave_ to return a local value.
|}]

(* with exclave_ it works *)
let foo (local_ x) = exclave_
  let y = id_local x in
  y
[%%expect{|
val foo : 'a @ [< 'm > local] -> 'a @ [> 'm | local dynamic] = <fun>
|}]

(* MULTIPLE MODE AXES *)

(* Bounds can be imposed on multiple axes *)
let foo (x @ uncontended portable) =
  use_uncontended (id x);
  use_portable (id x)
[%%expect{|
val foo : 'a @ [< global many portable uncontended] -> unit @ [> dynamic] =
  <fun>
|}]
